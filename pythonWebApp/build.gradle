buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies{
      classpath 'com.linkedin.pygradle:pygradle-plugin:0.+'
}
}

apply plugin: 'idea'
apply plugin: 'com.linkedin.python-sdist'

version = "0.1"

def webpackHome = "$projectDir/src/static"
def dockerHome = projectDir

python {
   docsDir = project.file('docs').path
   srcDir = project.file('src/server').path
   testDir = project.file('src/server/tests').path
   details {
     pythonVersion = '3.6'
     systemPythonInterpreter = file("/usr/bin/python3")
   }
}

dependencies {
    python 'pypi:requests:2.18.4'
}

repositories { pyGradlePyPi() }

task webpackInstall(type: Exec) {
    workingDir webpackHome
    inputs.file("$workingDir/package-lock.json")
    inputs.file("$workingDir/webpack.config.json")
    outputs.dir("$workingDir/node_modules")
    commandLine "npm", "install"
}

task webpackAssemble(type: Exec) {
    dependsOn webpackInstall
    workingDir webpackHome
    inputs.file("$workingDir/package-lock.json")
    inputs.file("$workingDir/webpack.config.json")
    inputs.dir("$workingDir/js")
    outputs.dir("$workingDir/dist")
    commandLine "npm", "run", "build"
}
tasks.assemble.dependsOn webpackAssemble

task webpackTest(type: Exec) {
    dependsOn webpackAssemble
    workingDir webpackHome
    inputs.file("$workingDir/package-lock.json")
    inputs.file("$workingDir/webpack.config.json")
    inputs.dir("$workingDir/js")
    inputs.dir("$workingDir/__tests__")
    outputs.upToDateWhen { true }
    commandLine "npm", "run", "test"
}
tasks.check.dependsOn webpackTest

task dockerBuild(type: Exec) {
    group "Docker"
    description "Construct a docker container from this project."
    dependsOn assemble
    workingDir dockerHome
    inputs.dir("$projectDir/src")
    inputs.file("$projectDir/Dockerfile")
    inputs.file("$projectDir/docker-compose.yml")
    inputs.file("$projectDir/pinned.txt")
    commandLine "docker-compose", "build"
}

task dockerStart(type: Exec) {
    group "Docker"
    description "Start an instance of this project."
    dependsOn dockerBuild
    workingDir dockerHome
    inputs.dir("$projectDir/src")
    inputs.file("$projectDir/Dockerfile")
    inputs.file("$projectDir/docker-compose.yml")
    inputs.file("$projectDir/pinned.txt")
    commandLine "docker-compose", "up", "--detach"
}

task dockerClean(type: Exec) {
    group "Docker"
    description "Stop and remove instances of this project."
    commandLine "docker-compose", "rm", "--stop", "--force"
}